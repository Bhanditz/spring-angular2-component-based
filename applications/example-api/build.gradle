import org.flywaydb.gradle.FlywayExtension
import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.flywaydb:flyway-gradle-plugin:3.2.1"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE")
    }
}

apply plugin: 'org.flywaydb.flyway'
apply plugin: 'spring-boot'

jar {
    baseName = 'example-api'
    version = '0.0.1'
}

apply from: "${getRootDir()}/java.gradle"

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-jdbc:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
    compile "org.postgresql:postgresql:9.4.1207"
    compile "com.zaxxer:HikariCP:2.4.3"
    compile "io.springfox:springfox-swagger2:2.0.0"
    compile "io.springfox:springfox-swagger-ui:2.0.0"

    compile project(":components/users")
    compile project(path: ":applications/front-end", configuration: "compile")
}

springBoot {
    mainClass = "com.github.joerodriguez.sbng2ex.ExampleApiApplication"
}

task migrateTestDatabase(type: FlywayMigrateTask) {
    extension = new FlywayExtension()
    extension.driver = 'org.postgresql.Driver'
    extension.url = 'jdbc:postgresql://localhost/spring-ng2-example-test'
    extension.user = 'admin'
    extension.password = 'admin'
}

task runAppTask(dependsOn: 'classes', type: JavaExec) {
    main = 'com.github.joerodriguez.tasks.TaskRunner'
    classpath = sourceSets.main.runtimeClasspath

    if (project.hasProperty('taskName')) {
        args(taskName)
    }
}

task copyFrontEnd(type: Copy) {
    from project(':applications/front-end').buildDir
    into "build/resources/main/public/"
}

processResources.dependsOn('copyFrontEnd')